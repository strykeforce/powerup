import jaci.openrio.gradle.GradleRIOPlugin
import jaci.openrio.gradle.frc.FRCJavaArtifact
import jaci.openrio.gradle.frc.RoboRIO

plugins {
    id 'java'
    id 'groovy'
    id 'jaci.openrio.gradle.GradleRIO' version '2018.03.06'
    id 'com.diffplug.gradle.spotless' version '3.10.0'
    id 'net.ltgt.apt-idea' version '0.15'
    id 'net.ltgt.errorprone' version "0.0.13"
}

def TEAM = 2767
def ROBOT_CLASS = 'frc.team2767.Robot'

deploy {
    targets {
        target("roborio", RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', FRCJavaArtifact) {
            targets << "roborio"
        }
    }
}

repositories {
    jcenter()
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } // nanohttpd
}

dependencies {
    //noinspection GroovyAssignabilityCheck
    compile wpilib()
    compile ctre()
    compile navx()
    compile openrio.powerup.matchData()
    compile pathfinder()
    compile 'org.strykeforce.thirdcoast:core:18.5.0'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile group: 'com.j2html', name: 'j2html', version: '1.2.2'
    compile 'com.google.dagger:dagger:2.14.1'

    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'
    annotationProcessor 'com.google.auto.factory:auto-factory:1.0-beta5'

    compileOnly 'org.jetbrains:annotations:15.0'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly('com.google.auto.factory:auto-factory:1.0-beta5') {
        exclude(group: 'com.google.guava') // https://github.com/google/dagger/issues/897
    }

    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testRuntime 'org.objenesis:objenesis:2.6'
    testRuntime 'net.bytebuddy:byte-buddy:1.7.10'
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

spotless {
    java {
        googleJavaFormat()
        target '**/*.java'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}
